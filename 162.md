Magic Cobalt Giraffe

Medium

# USDC and WETH withdrawals through `L2GatewayRouter.sol` will fail

## Summary
USDC and WETH withdrawals through `L2GatewayRouter.sol` will fail, as users are supposed to give approval to the router and not the corresponding gateways

## Vulnerability Detail
To withdraw ERC20 back to L1, the following functions are typically used in `L2GatewayRouter.sol` by the user ðŸ‘ 
```solidity
    function withdrawERC20(address _token, uint256 _amount, uint256 _gasLimit) external payable override {
        withdrawERC20AndCall(_token, _msgSender(), _amount, new bytes(0), _gasLimit);
    }

    /// @inheritdoc IL2ERC20Gateway
    function withdrawERC20(address _token, address _to, uint256 _amount, uint256 _gasLimit) external payable override {
        withdrawERC20AndCall(_token, _to, _amount, new bytes(0), _gasLimit);
    }

    /// @inheritdoc IL2ERC20Gateway
    function withdrawERC20AndCall(
        address _token,
        address _to,
        uint256 _amount,
        bytes memory _data,
        uint256 _gasLimit
    ) public payable override onlyNotInContext {
        address _gateway = getERC20Gateway(_token);
        require(_gateway != address(0), "no gateway available");

        // enter deposit context
        gatewayInContext = _gateway;
        // encode msg.sender with _data
        bytes memory _routerData = abi.encode(_msgSender(), _data);

        IL2ERC20Gateway(_gateway).withdrawERC20AndCall{value: msg.value}(_token, _to, _amount, _routerData, _gasLimit);
        // leave deposit context
        gatewayInContext = address(0);
    }
```
In the USDC gateway, `withdrawERC20AndCall()` is called, which calls the `_withdraw()` function

```solidity
   function _withdraw(
        address _token,
        address _to,
        uint256 _amount,
        bytes memory _data,
        uint256 _gasLimit
    ) internal virtual override nonReentrant {
        require(_amount > 0, "withdraw zero amount");
        require(_token == l2USDC, "only USDC is allowed");
        require(!withdrawPaused, "withdraw paused");

        // 1. Extract real sender if this call is from L2GatewayRouter.
        address _from = _msgSender();
        if (router == _from) {
            (_from, _data) = abi.decode(_data, (address, bytes));
        }
        require(_data.length == 0, "call is not allowed");

        // 2. Transfer token into this contract.
        IERC20Upgradeable(_token).safeTransferFrom(_from, address(this), _amount);
        IFiatToken(_token).burn(_amount);

        // 3. Generate message passed to L1USDCGateway.
        address _l1USDC = l1USDC;
        bytes memory _message = abi.encodeCall(
            IL1ERC20Gateway.finalizeWithdrawERC20,
            (_l1USDC, _token, _from, _to, _amount, _data)
        );

        uint256 nonce = IL2CrossDomainMessenger(messenger).messageNonce();
        // 4. Send message to L2CrossDomainMessenger.
        IL2CrossDomainMessenger(messenger).sendMessage{value: msg.value}(counterpart, 0, _message, _gasLimit);

        emit WithdrawERC20(_l1USDC, _token, _from, _to, _amount, _data, nonce);
    }
```

As you can see, the function directly attempts to transfer USDC from the `_from` address here, however, the approval is actually given to the router contract by the user and the function is supposed to call `router.requestERC20()` instead, just like it is done in other gateways. This will cause an unexpected revert even though the user has already approved the required amount to the router.

The same vulnerability exists in `L2WETHGateway.sol` as well.
## Impact
Breaks core functionality, no loss of funds
## Code Snippet
https://github.com/sherlock-audit/2024-08-morphl2/blob/98e0ec4c5bbd0b28f3d3a9e9159d1184bc45b38d/morph/contracts/contracts/l2/gateways/usdc/L2USDCGateway.sol#L168
## Tool used

Manual Review

## Recommendation
Use this function which is used in other gateways to transfer the USDC tokens in `_withdraw()` : 
```solidity

    function _transferERC20In(
        address _token,
        uint256 _amount,
        bytes memory _data
    ) internal returns (address, uint256, bytes memory) {
        address _sender = _msgSender();
        address _from = _sender;
        if (router == _sender) {
            // Extract real sender if this call is from L1GatewayRouter.
            (_from, _data) = abi.decode(_data, (address, bytes));
            _amount = IL2GatewayRouter(_sender).requestERC20(_from, _token, _amount);
        } else {
            // common practice to handle fee on transfer token.
            uint256 _before = IERC20Upgradeable(_token).balanceOf(address(this));
            IERC20Upgradeable(_token).safeTransferFrom(_from, address(this), _amount);
            uint256 _after = IERC20Upgradeable(_token).balanceOf(address(this));
            // no unchecked here, since some weird token may return arbitrary balance.
            _amount = _after - _before;
        }
        // ignore weird fee on transfer token
        require(_amount > 0, "withdraw zero amount");

        return (_from, _amount, _data);
    }
```